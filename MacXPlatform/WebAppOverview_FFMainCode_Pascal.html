<!DOCTYPE html>
<!--Copyright 2018 Phil Hess-->
<html>

<head>
<title></title>
<style type="text/css">
  BODY { font: 13px/18px "Lucida Grande", Arial, sans-serif; background-color: #fff; color: #333; }
</style>

<meta name="AUTHOR" content="Phil Hess">
<meta name="viewport" content="width=device-width, minimum-scale=1.0, maximum-scale=1.6">
</head>

<link rel="stylesheet" href="highlight/styles/default.css">
<script src="highlight/highlight.pack.js"></script>
<script>hljs.initHighlightingOnLoad();</script>

<body>
<pre><code class="delphi">unit MainPage;

interface

{$modeswitch externalclass}

uses
  JS,
  qx,
  qx.event,
  qx.data,
  qx.ui.mobile;

type
  TMainPage = class external name 'fishfacts.MainPage' (TPage_NavigationPage)
  end;


implementation

procedure Construct;
begin
  TMainPage(JSThis).base(JSArguments, nil);
  TMainPage(JSThis).setTitle('Fish Facts');
end;


procedure Initialize;

  procedure BindingErrorHandler(evt : TType_Data);
  begin
    TDialog_Manager.getInstance.alert(
     'Error', 'Error loading biolife.json', nil, nil, 'OK');
  end;

  procedure ListConfigureItem(item  : TRenderer_Default;
                              value : TCore_Object;
                              row   : Integer);
  begin
    item.setTitle(string(value.get('Common_Name')));
    item.setShowArrow(True);
  end;

  procedure OnChangeSelection(evt : TType_Data);
  begin
    TMainPage(JSThis).fireDataEvent('showDetails', evt.getData);
  end;

var
  this  : TMainPage;
  url   : string;
  store : TStore_Json;
  list  : TList_List;
begin
  this := TMainPage(JSThis);

  this.base(JSArguments, nil);

  url := 'resource/fishfacts/biolife.json';

  store := TStore_Json.New(url);

  store.bind('model', this, 'allData', nil);

  store.addListener('error', @BindingErrorHandler, this);

  list := TList_List.New;

  list.setDelegate(new(['configureItem', @ListConfigureItem]));

  list.addListener('changeSelection', @OnChangeSelection, this);

  this.bind('allData', list, 'model', nil);

  this.getContent.add(list);
end;  {Initialize}


procedure Destruct;
begin
end;


initialization

  TQx_Class.define('fishfacts.MainPage',
    new(['extend', TPage_NavigationPage,

         'construct', @Construct,

         'events',
           new(['showDetails', 'qx.event.type.Data']),

         'properties', 
           new(['allData', new(['check', 'qx.data.Array',
                                'nullable', True,
                                'init', nil,
                                'event', 'loadData'])]),

         'members',
           new(['_initialize', @Initialize]),

         'destruct', @Destruct]));

end.
</code></pre>

</body>
</html>
